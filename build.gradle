plugins {
	id 'net.fabricmc.fabric-loom-remap' version "${loom_version}"
	id 'maven-publish'
	id 'signing'
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {
	mavenCentral()
	maven {
		url = "https://maven.fabricmc.net"
	}
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings loom.officialMojangMappings()
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": inputs.properties.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

java {
	withSourcesJar()
	withJavadocJar()

	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

jar {
	inputs.property "archivesName", project.base.archivesName

	from("LICENSE") {
		rename { "${it}_${inputs.properties.archivesName}"}
	}
}

// configure the maven publication
publishing {
    publications {
        create("mavenJava", MavenPublication) {
            artifactId = project.archives_base_name
            from components.java

			pom {
				name = "GmLrdLib"
				description = "Some useful code from GameLord2011"
				url = "https://github.com/gamelord2011/gmlrdlib"
				licenses {
					license {
						name = "Mit License"
						url = "https://opensource.org/license/mit"
					}
				}
				developers {
					developer {
						id = 'gamelord2011'
						name = 'GameLord2011'
					}
				}
                withXml {
                    def repos = asNode().appendNode('repositories')
                    def repo = repos.appendNode('repository')
                    repo.appendNode('id', 'fabricmc')
                    repo.appendNode('url', 'https://maven.fabricmc.net/')
                }
				scm {
					connection = 'scm:git:git://github.com/gamelord2011/gmlrdlib.git'
					developerConnection = 'scm:git:ssh://github.com/gamelord2011/gmlrdlib.git'
					url = 'https://github.com/gamelord2011/gmlrdlib/'
				}
            }
        }
    }

    repositories {
        maven {
            url = layout.buildDirectory.dir("repo")
        }
    }
}

signing {
	useGpgCmd()
    sign publishing.publications.mavenJava
}
